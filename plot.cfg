filename = "graph.txt"

set style data lines
set style line 1 lc rgb '#09ad00' pt 6 ps 1 lt 1 lw 2 # --- green
set style line 2 lc rgb '#8b1a0e' pt 1 ps 1 lt 1 lw 2 # --- red
set style line 3 linetype 1 pointtype 4 lc rgb '#0025ad' ps 1 lw 2 # --- blue

set style line 11 lc rgb '#808080' lt 1
set border 3 back ls 11
set arrow from graph 1,graph 1 to graph 1,graph 0 nohead ls 3

set xtics nomirror
set y2tics offset 0,0
set y2tics textcolor ls 3
set term unknown
plot filename u 1:2, filename u 1:3
factor = 4
set y2range[GPVAL_Y_MIN*factor:GPVAL_Y_MAX*factor]
set key left top

set style line 12 lc rgb '#808080' lt 0 lw 1
set grid back ls 12

# png
set terminal pngcairo size 1000,600 enhanced font 'Verdana,10'
set output 'graph.png'

# wxt
#set terminal wxt size 1000,600 enhanced font 'Verdana,10' persist

plot filename u 1:2 t 'added' ls 1 with filledcurves y1=0 fs transparent solid .5, filename u 1:3 t 'removed' ls 2 with filledcurves y1=0 fs transparent solid .5, filename u 1:4 t 'total' ls 3 axes x1y2 with linespoints

